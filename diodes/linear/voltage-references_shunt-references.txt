*
*Zetex ZRC250 Spice Model v1.0 Last Revised 11/07/06
*
*NOTE: This is a simplified model.  Confirm
*any design using a physical circuit.
*
.SUBCKT ZRC250 1 3
*Connections  Vz Gnd
*
*Quiescent current
R1 1 49 320E3
R2 49 3 315.2E3
C1 49 1 4.5e-12
E1 50 3 49 3 1
D8 50 51 Dmod1
Rq 51 52 Rmod2 70E3
Vq 52 3 0
F1 1 3 Vq 1
*
*Reference voltage
Iref 3 21 1.2405E-3
Rref 21 3 Rmod1 1000
*
*Gain
G2 3 31 49 21 1e-7
Rt2  3 31 100E6
Rt3  3 32 3E6
Ct1 31 32 1.5E-13
D3 31 3  Dmod1
D4 3 31  Dmod1
*
*Output
G3 41 3 31 3 0.3
Rc1 6 5 10
D6 3 41 Dmod1
D7 3 6 Dmod1
L1 1 6 10nH
D9 5 41 Dmod2
*
.MODEL Qmod1 NPN BF=220 CJC=3E-12 CJE=3E-12
.MODEL Rmod1 RES (TC1=3.8e-5 TC2=-1e-7)
.MODEL Rmod2 RES (TC1=1.7E-3 TC2=0)
.MODEL Dmod1 D IS=5E-15 RS=4 BV=22
.MODEL Dmod2 D IS=1E-18 RS=.01
.ENDS  ZRC250
*
*$
*
*                (c)  2006 Zetex Semiconductors plc
*
*   The copyright in these models  and  the designs embodied belong
*   to Zetex Semiconductors plc (" Zetex ").  They  are  supplied
*   free of charge by Zetex for the purpose of research and design
*   and may be used or copied intact  (including this notice)  for
*   that purpose only.  All other rights are reserved. The models
*   are believed accurate but  no condition  or warranty  as to their
*   merchantability or fitness for purpose is given and no liability
*   in respect of any use is accepted by Zetex PLC, its distributors
*   or agents.
*
*   Zetex Semiconductors plc, Zetex Technology Park, Chadderton,
*   Oldham, United Kingdom, OL9 9LL